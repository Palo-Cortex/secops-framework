fromversion: 6.10.0
rule_id: 0
action: ALERTS
alert_category: OTHER
alert_description: $alert_description
alert_domain: DOMAIN_SECURITY
alert_fields:
  action_file_path: filepath
  action_file_sha256: sha256
  action_local_ip: local_ip
  action_remote_ip: remote_ip_str
  actor_effective_username: user_name
  actor_process_command_line: cmdline
  actor_process_image_name: filename
  actor_process_image_path: filepath
  actor_process_image_sha256: sha256
  additionalindicators: ioc_value
  agent_device_domain: domain
  agent_hostname: v1_host_name
  agent_id: v1_host_guid
  alert_description: alert_description
  external_pivot_url: workbench_link
  externallink: workbench_link
  externalstatus: status
  filehash: sha256
  mac: mac_address
  mitretechniqueid: mitre_ids_str
  originalalertid: id
  originalalertname: alert_name
  originalalertsource: alert_source
  parentprocessname: parent_process_name
  parentprocesspath: parent_process_path
  prenatsourceip: local_ip
  processcmd: cmdline
  severity: severity
  source_insert_ts: alert_time
  tim_main_indicator: ioc_value
  trendmicrovisiononexdrindicators: indicators_json
  trendmicrovisiononexdrindicatorsjson: indicators_json
  trendmicrovisiononexdrinvestigationstatus: investigation_status
  trendmicrovisiononexdrpriorityscore: score
  userid: user_id
alert_name: Trend Micro - $alert_name
alert_type: null
crontab: null
dataset: alerts
description: null
drilldown_query_timeframe: ALERT
execution_mode: REAL_TIME
global_rule_id: c6eeabcc-3842-40e5-b327-7989f7f835be_ta0003_ta0009
investigation_query_link: ''
lookup_mapping: []
mapping_strategy: CUSTOM
mitre_defs:
  TA0009 - Collection: []
name: SOC Trend Micro Vision One V3 - Collection
search_window: null
severity: User Defined
suppression_duration: null
suppression_enabled: false
suppression_fields: null
user_defined_category: null
user_defined_severity: severity
xql_query: |
  dataset = trend_micro_vision_one_v3_generic_alert_raw
  | filter alert_provider = "SAE"

  | alter j = _alert_data -> raw_json

  /* --- MITRE technique (cheap: first rule/filter only) --- */
  | alter j_str = to_string(j)
  | alter mitre_technique_id_raw =
      json_extract_scalar(j_str, "$.matched_rules[0].matched_filters[0].mitre_technique_ids[0]")
  | alter j_str = null

  | alter mitre_ids_str =
      if(
        mitre_technique_id_raw != null and mitre_technique_id_raw != "",
        replace(replace(mitre_technique_id_raw, "\"",""), "\\.[0-9]+$",""),
        "â€”"
      )

  /* --- Strip sub-technique: T1547.001 -> T1547 (only if it has a dot) --- */
  | alter mitre_ids_str =
      if(mitre_ids_str contains ".", arrayindex(regextract(mitre_ids_str, "(.*)\."), 0), mitre_ids_str)

  /* --- MITRE Tactics Arrays ---- */
  | alter ta0043_reconnaissance       = arraycreate("T1590","T1591","T1592","T1593","T1594","T1595","T1596","T1597","T1598","T1599")
  | alter ta0042_resource_development = arraycreate("T1583","T1584","T1585","T1586","T1587","T1650")
  | alter ta0001_initial_access       = arraycreate("T1078","T1189","T1190","T1195","T1133","T1200","T1566","T1091")
  | alter ta0002_execution            = arraycreate("T1059","T1106","T1047","T1203","T1129","T1559")
  | alter ta0003_persistence          = arraycreate("T1547","T1543","T1136","T1505","T1053","T1078")
  | alter ta0004_privilege_escalation = arraycreate("T1548","T1068","T1078","T1055","T1134")
  | alter ta0005_defense_evasion      = arraycreate("T1027","T1070","T1218","T1140","T1562","T1036","T1055")
  | alter ta0006_credential_access    = arraycreate("T1003","T1555","T1552","T1110","T1621")
  | alter ta0007_discovery            = arraycreate("T1082","T1083","T1046","T1057","T1016","T1049","T1033")
  | alter ta0008_lateral_movement     = arraycreate("T1021","T1210","T1091","T1072")
  | alter ta0009_collection           = arraycreate("T1005","T1039","T1113","T1114","T1115")
  | alter ta0011_command_and_control  = arraycreate("T1071","T1095","T1105","T1571","T1572","T1041")
  | alter ta0010_exfiltration         = arraycreate("T1041","T1567","T1020")
  | alter ta0040_impact               = arraycreate("T1485","T1486","T1490","T1499","T1561")

  /* --- Match Tactic Name (Impact -> Recon, no overwrite) --- */
  | alter mitre_tactic = if (ta0040_impact contains mitre_ids_str, "Impact")
  | alter mitre_tactic = if (ta0010_exfiltration contains mitre_ids_str, "Exfiltration", mitre_tactic)
  | alter mitre_tactic = if (ta0011_command_and_control contains mitre_ids_str, "Command and Control", mitre_tactic)
  | alter mitre_tactic = if (ta0009_collection contains mitre_ids_str, "Collection", mitre_tactic)
  | alter mitre_tactic = if (ta0008_lateral_movement contains mitre_ids_str, "Lateral Movement", mitre_tactic)
  | alter mitre_tactic = if (ta0007_discovery contains mitre_ids_str, "Discovery", mitre_tactic)
  | alter mitre_tactic = if (ta0006_credential_access contains mitre_ids_str, "Credential Access", mitre_tactic)
  | alter mitre_tactic = if (ta0005_defense_evasion contains mitre_ids_str, "Defense Evasion", mitre_tactic)
  | alter mitre_tactic = if (ta0004_privilege_escalation contains mitre_ids_str, "Privilege Escalation", mitre_tactic)
  | alter mitre_tactic = if (ta0003_persistence contains mitre_ids_str, "Persistence", mitre_tactic)
  | alter mitre_tactic = if (ta0002_execution contains mitre_ids_str, "Execution", mitre_tactic)
  | alter mitre_tactic = if (ta0001_initial_access contains mitre_ids_str, "Initial Access", mitre_tactic)
  | alter mitre_tactic = if (ta0042_resource_development contains mitre_ids_str, "Resource Development", mitre_tactic)
  | alter mitre_tactic = if (ta0043_reconnaissance contains mitre_ids_str, "Reconnaissance", mitre_tactic)

  /* --- Match Tactic ID (Impact -> Recon, no overwrite) --- */
  | alter mitre_tactic_id = if (ta0040_impact contains mitre_ids_str, "TA0040")
  | alter mitre_tactic_id = if (ta0010_exfiltration contains mitre_ids_str, "TA0010", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0011_command_and_control contains mitre_ids_str, "TA0011", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0009_collection contains mitre_ids_str, "TA0009", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0008_lateral_movement contains mitre_ids_str, "TA0008", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0007_discovery contains mitre_ids_str, "TA0007", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0006_credential_access contains mitre_ids_str, "TA0006", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0005_defense_evasion contains mitre_ids_str, "TA0005", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0004_privilege_escalation contains mitre_ids_str, "TA0004", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0003_persistence contains mitre_ids_str, "TA0003", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0002_execution contains mitre_ids_str, "TA0002", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0001_initial_access contains mitre_ids_str, "TA0001", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0042_resource_development contains mitre_ids_str, "TA0042", mitre_tactic_id)
  | alter mitre_tactic_id = if (ta0043_reconnaissance contains mitre_ids_str, "TA0043", mitre_tactic_id)

  /* ---- Split Anchor (required by mitre split script) ---- */
  | alter
      mitre_technique_id = mitre_ids_str,
      mitre_technique    = null,
      mitre_tactic_id    = mitre_tactic_id,
      mitre_tactic       = mitre_tactic
  | filter mitre_tactic_id = "TA0009" or mitre_tactic = "Collection"

  /* ---- Core metadata ---- */
  | alter
      id                   = j -> id,
      status               = j -> status,
      investigation_status = j -> investigation_status,
      investigation_result = j -> investigation_result,
      workbench_link       = j -> workbench_link,
      alert_provider       = j -> alert_provider,
      alert_name           = j -> model,
      score                = to_integer(j -> score),
      severity             = j -> severity,
      alert_time           = j -> created_date_time,
      alert_description    = j -> description

  | alter
      indicators = j -> indicators[],
      entities   = j -> impact_scope.entities[]

  /* ---- Host / User ---- */
  | alter ent_hosts = arrayfilter(entities, json_extract_scalar("@element","$.entity_type") = "host")
  | alter ent_users = arrayfilter(entities, json_extract_scalar("@element","$.entity_type") = "user")

  | alter host0 = arrayindex(ent_hosts, 0)

  | alter
      v1_host_guid = json_extract_scalar(host0, "$.entity_value.guid"),
      v1_host_name = json_extract_scalar(host0, "$.entity_value.name"),
      local_ip     = replace(json_extract_scalar(host0, "$.entity_value.ips[0]"), "\"", "")

  | alter mac_address =
      coalesce(
        json_extract_scalar(host0, "$.entity_value.mac"),
        json_extract_scalar(host0, "$.entity_value.mac_address"),
        json_extract_scalar(host0, "$.entity_value.macs[0]"),
        json_extract_scalar(host0, "$.entity_value.macAddresses[0]")
      )

  | alter user0 =
      arrayindex(
        arrayfilter(
          entities,
          json_extract_scalar("@element","$.entity_type") = "user"
          or json_extract_scalar("@element","$.entity_type") = "account"
        ),
        0
      )

  | alter
      user_name =
        coalesce(
          json_extract_scalar(user0, "$.entity_value.name"),
          json_extract_scalar(user0, "$.entity_value.username"),
          json_extract_scalar(user0, "$.entity_value") /* handles account string */
        ),
      user_id =
        coalesce(
          json_extract_scalar(user0, "$.entity_value.id"),
          json_extract_scalar(user0, "$.entity_value.userId"),
          json_extract_scalar(user0, "$.entity_value.sid"),
          json_extract_scalar(user0, "$.entity_id") /* fallback */
        )

  /* ---- Indicators (minimal set) ---- */
  /* extract each indicator object once */
  | alter
  cmd0  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.type")  = "command_line"), 0),
  reg0  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.field") = "objectRegistryData"), 0),
  peer0 = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.field") = "peerIp"), 0),
  pfp0  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.field") = "parentFilePath"), 0),
  sha0  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.type")  = "file_sha256"), 0),
  md50  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.type")  = "file_md5"), 0),
  dom0  = arrayindex(arrayfilter(indicators, json_extract_scalar("@element","$.field") = "domain"), 0)

  /* values from the extracted indicator objects */
  | alter
  cmdline            = json_extract_scalar(cmd0,  "$.value"),
  reg_path           = json_extract_scalar(reg0,  "$.value"),
  remote_ip_str      = json_extract_scalar(peer0, "$.value"),
  parent_process_path= json_extract_scalar(pfp0,  "$.value"),
  sha256             = json_extract_scalar(sha0,  "$.value"),
  md5                = json_extract_scalar(md50,  "$.value"),
  domain             = json_extract_scalar(dom0,  "$.value")

  /* filepath + derived */
  | alter filepath =
  coalesce(
    reg_path,
    arrayindex(regextract(cmdline, "^\\s*([^\\s]+)"), 0)
  )
  | alter
  filename = replace(filepath, "^.*[\\\\/]", ""),
  parent_process_name = replace(parent_process_path, "^.*[\\\\/]", "")

  /* convenience */
  | alter
  ioc_value       = coalesce(sha256, md5),
  alert_source    = coalesce(alert_provider, "Trend Micro Vision One"),
  indicators_json = to_string(indicators)


  | fields
      id, workbench_link, alert_name, alert_source, status,
      investigation_status, investigation_result,
      score, severity, alert_time, alert_description,
      v1_host_guid, v1_host_name, local_ip, mac_address,
      user_name, user_id,
      filename, filepath, parent_process_path, parent_process_name, cmdline,
      sha256, ioc_value, domain, remote_ip_str,
      indicators_json,
      mitre_technique, mitre_technique_id, mitre_tactic, mitre_tactic_id, mitre_ids_str
